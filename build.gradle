//------------------------------------------------------

plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
}

//------------------------------------------------------

String nome      = 'Uxi-amarelo'
String descricao = 'Fachada Web para Copa√≠bas.'

group   = 'com.joseflavio'
version = '1.0-A14'

//------------------------------------------------------

float  javaVersao  = 1.8
String codificacao = 'UTF-8'

sourceCompatibility = javaVersao
targetCompatibility = javaVersao

tasks.withType(JavaCompile) {
    options.encoding = codificacao
}

//------------------------------------------------------

repositories {
    mavenCentral()
}

//------------------------------------------------------

dependencies {
    implementation fileTree(dir:'lib', include:['*.jar'])
    implementation 'com.joseflavio:copaiba:1.0-A17'
    implementation('com.ibm.etcd:etcd-java:0.0.18'){
        exclude group: 'io.netty'
    }
    implementation 'org.omnifaces:omnifaces:3.4.1'
    compileOnly 'org.apache.tomee:javaee-api:8.0-5'
    implementation 'commons-io:commons-io:2.8.0'
    implementation 'org.apache.commons:commons-lang3:3.11'
    testImplementation 'junit:junit:4.13'
}

//------------------------------------------------------

jar {
    manifest {
        attributes 'Implementation-Title': nome,
                   'Implementation-Version': archiveVersion,
                   'Class-Path': configurations.runtimeClasspath.collect{ it.getName() }.join(' ')
    }
}

//------------------------------------------------------

java {
    withSourcesJar()
    withJavadocJar()
}

//------------------------------------------------------

javadoc {
    if( JavaVersion.current().isJava9Compatible() ){
        options.addBooleanOption('html5', true)
    }
}

//------------------------------------------------------

task copiarDeps(type: Copy) {
    from configurations.runtimeClasspath
    into "$buildDir/deps"
}

//------------------------------------------------------

task warHtml(type: Copy) {
    from "$projectDir/web"
    into "$buildDir/web"
}

task warJava(type: Copy, dependsOn: [compileJava]) {
    from sourceSets.main.output.classesDirs
    into "$buildDir/web/WEB-INF/classes"
}

task warRec(type: Copy) {
    from sourceSets.main.output.resourcesDir
    into "$buildDir/web/WEB-INF/classes"
}

task warDep(type: Copy, dependsOn: [copiarDeps]) {
    from "$buildDir/deps"
    into "$buildDir/web/WEB-INF/lib"
    exclude "groovy*"
    exclude "javax.*"
    exclude "jsr*"
    exclude "slf4j*"
}

task war(type: Zip, dependsOn: [warHtml, warJava, warRec, warDep]) {
    archiveName = rootProject.name + ".war"
    destinationDir = file("$buildDir")
    from "$buildDir/web"
    into ""
    includeEmptyDirs = true
}

task dist(dependsOn: [war]) {
}

build.dependsOn dist

//------------------------------------------------------

task tomcatRemover(type: Delete) {
    nome = "$System.env.CATALINA_HOME/webapps/" + rootProject.name + ".war"
    if( file(nome).exists() ){
        delete nome
    }
}

task tomcat(type: Copy, dependsOn: [tomcatRemover, war]) {
    from "$buildDir/" + rootProject.name + ".war"
    into "$System.env.CATALINA_HOME/webapps"
}

//------------------------------------------------------

eclipse {
    project {
        name    = rootProject.name
        comment = descricao
    }
    jdt {
        sourceCompatibility = javaVersao
        targetCompatibility = javaVersao
    }
}

eclipseJdt {
    doLast {
        file('.settings/org.eclipse.core.resources.prefs').text =
            'eclipse.preferences.version=1\nencoding/<project>=' + codificacao
    }
}

//------------------------------------------------------

idea {
    project {
        languageLevel = new org.gradle.plugins.ide.idea.model.IdeaLanguageLevel(javaVersao)
        jdkName = '' + javaVersao
        vcs = 'Git'
        ipr {
            withXml {
                def projeto  = it.asNode()
                def encoding = projeto.component.find { it.@name == 'Encoding' }
                encoding.appendNode('file', [url: 'PROJECT', charset: codificacao])
            }
        }
    }
}

//------------------------------------------------------